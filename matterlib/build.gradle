apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    defaultPublishConfig "solidRelease"
    // this is the default flavor and is accessible at these coordinates:
    // for a project that builds a single library, it will be at these coordinates:
    // "com.github.<user-name>:<repository-name>:<github-tag>
    //
    // Example for this particular demo:
    // implementation "com.github.mirceanis:multi-flavor-lib-demo:0.0.7-single-lib"

    flavorDimensions "solidity"

    productFlavors {
        liquid {
            dimension "solidity"
            //for a project that builds a single library, the flavor will be at these coordinates:
            // "com.github.<user-name>:<repository-name>:<github-tag>:<flavor>@aar
            //
            // Example for this particular demo and flavor:
            // implementation "com.github.mirceanis:multi-flavor-lib-demo:0.0.7-single-lib:liquid@aar"
        }
        solid {
            dimension "solidity"
            //for a project that builds a single library, the flavor will be at these coordinates:
            // "com.github.<user-name>:<repository-name>:<github-tag>:<flavor>@aar
            //
            // Example for this particular demo and flavor:
            // implementation "com.github.mirceanis:multi-flavor-lib-demo:0.0.7-single-lib:solid@aar"
        }
    }

}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.javaDirectories
}

artifacts {
    archives sourcesJar
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

group = 'com.github.mirceanis.multi-flavor-lib-demo'
version = library_version

if (android.productFlavors.size() > 0) {
    android.libraryVariants.all { variant ->

        if (variant.name.toLowerCase().contains("debug")) {
            return
        }

        def bundleTask = tasks["bundle${variant.name.capitalize()}Aar"]

        artifacts {
            archives(bundleTask.archivePath) {
                classifier variant.flavorName
                builtBy bundleTask
                name = project.name
            }
        }

        def flavorName = variant.flavorName.toLowerCase()
        def flavorSourcesTask = tasks.create(name : "${flavorName}SourcesJar", type: Jar) {
            classifier = "${flavorName}-sources"
            from android.sourceSets.main.javaDirectories
            from android.sourceSets[flavorName].javaDirectories
        }

        artifacts {
            archives flavorSourcesTask
        }

    }
}
